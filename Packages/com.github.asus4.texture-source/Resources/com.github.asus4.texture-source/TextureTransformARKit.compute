#pragma kernel TextureTransform

#include "Common.hlsl"

Texture2D<float4> _textureY;
Texture2D<float4> _textureCbCr;

RWTexture2D<float4> _OutputTex;
uint2 _OutputTexSize;
float4x4 _TransformMatrix;

SamplerState linearClampSampler;

[numthreads(8,8,1)]
void TextureTransform (uint2 id : SV_DispatchThreadID)
{
    if(any(id >= _OutputTexSize))
    {
        return; 
    }

    float2 uv = (float2)id / float2(_OutputTexSize - 1.0);
    uv = mul(_TransformMatrix, float4(uv, 0, 1)).xy;

    float3 c = YCbCrToSRGB(
        _textureY.SampleLevel(linearClampSampler, uv, 0).x,
        _textureCbCr.SampleLevel(linearClampSampler, uv, 0).xy
    );

    // Linear color support
    #ifndef UNITY_NO_LINEAR_COLORSPACE
    c = FastSRGBToLinear(c);
    #endif

    _OutputTex[id] = any(uv < 0) || any(uv > 1)
        ? float4(0, 1, 0, 1)
        : float4(c, 1);
}
